buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath group: 'com.diluv.schoomp', name: 'Schoomp', version: '1.1.0+'
    }
}

apply from: "https://raw.githubusercontent.com/Flemmli97/GradleScripts/mc-architectury-fabric-neoforge/changelog.gradle"

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"

    loom {
        silentMojangMappingsLicense()
    }

    repositories {
        maven { url = 'https://maven.parchmentmc.org' }
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings loom.layered() {
            officialMojangMappings()
            parchment("org.parchmentmc.data:parchment-${rootProject.parchment_version}@zip")
        }
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    
    archivesBaseName = rootProject.archives_base_name
    version = project.minecraft_version + "-" + rootProject.mod_version
    group = rootProject.maven_group

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = project.hasProperty('jvm') ? rootProject.jvm : 21
    }

    java {
        withSourcesJar()
    }
}

task buildFabric {
    dependsOn(":fabric:build")
}

task cleanFabric {
    dependsOn clean
    dependsOn(":fabric:clean")
}

task buildToLocal {
    dependsOn cleanFabric, buildFabric, ":fabric:publishToMavenLocal"
    buildFabric.mustRunAfter cleanFabric
}
